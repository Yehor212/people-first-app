// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath 'com.google.gms:google-services:4.4.4'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects { project ->
    afterEvaluate {
        if (project.plugins.hasPlugin('com.android.application') ||
            project.plugins.hasPlugin('com.android.library')) {
            project.android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }

        // Set Kotlin JVM target to match Java 17
        if (project.plugins.hasPlugin('org.jetbrains.kotlin.android')) {
            project.android.kotlinOptions {
                jvmTarget = '17'
            }
        }
    }

    // Force compatible dependency versions for AGP 8.2.2
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.12.0'
            force 'androidx.core:core-ktx:1.12.0'
            force 'androidx.activity:activity:1.8.2'
            force 'androidx.activity:activity-ktx:1.8.2'
            force 'androidx.core:core-splashscreen:1.0.1'
            force 'androidx.appcompat:appcompat:1.6.1'
            force 'androidx.fragment:fragment:1.6.2'
            force 'androidx.fragment:fragment-ktx:1.6.2'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
